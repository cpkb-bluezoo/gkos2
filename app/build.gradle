plugins {
    id 'com.android.application'
}

// Load release signing config from keystore.properties (not in VCS)
def keystorePropertiesFile = rootProject.file('keystore.properties')
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'org.bluezoo.gkos'
    compileSdk 34

    defaultConfig {
        applicationId 'org.bluezoo.gkos'
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName '0.1.0'
    }

    if (keystorePropertiesFile.exists()) {
        signingConfigs {
            release {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    // IME uses framework APIs only; no appcompat required
}
